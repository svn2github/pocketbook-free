var
  // часы
  Digit, ySlide: array[0..8] of integer; 

  // вагоны
  VagonCount: integer;
  tel1, tel2: array[0..3] of integer;
  VagonName: array[0..3] of string;
  WheelBase: extended;
  MinTime, MaxTime: extended;

  t: single;
  StopTime: integer;
  Period: integer;


function Path2Segment(TrainNumber: integer; L: extended): integer;
var LCalc, HCalc, iCalc: integer;
begin
  //PathLength и PathLength2 - это единый массив, поэтому если TrainNumber=1 добавляем NodeCount к SFrom, STo
  LCalc := NodeCount * TrainNumber;
  HCalc := (NodeCount - 1) + NodeCount * TrainNumber;

  // ищем, в каком сегменте половинным делением
  if PathLength[HCalc] <= L then
    LCalc := HCalc;
  while LCalc < HCalc - 1 do begin
    iCalc := (LCalc + HCalc) shr 1;
    if PathLength[iCalc] <= L then
      LCalc := iCalc
    else
      HCalc := iCalc;
  end;
  Result := LCalc;

//    Result := Result - NodeCount * TrainNumber;
end; // Path2Segment

function Segment2Path(TrainNumber: integer; Segment: extended): extended;
var iSegment: integer;
    SegmentPart: extended;
begin
  iSegment := Trunc(Segment);
  if iSegment < 0 then
    iSegment := 0
  else if iSegment > NodeCount - 1 then
    iSegment := NodeCount - 1;
  SegmentPart := Segment - iSegment;

  if (iSegment <= (TrainNumber + 1) * NodeCount - 1) or (SegmentPart <= 0) then
    Result := PathLength[iSegment + TrainNumber * NodeCount] + SegmentPart * (PathLength[iSegment + 1 + TrainNumber * NodeCount] - PathLength[iSegment + TrainNumber * NodeCount])
  else
    Result := PathLength[iSegment + TrainNumber * NodeCount] + SegmentPart * (PathLength[iSegment + TrainNumber * NodeCount] - PathLength[iSegment - 1 + TrainNumber * NodeCount]);
end; // Segment2Path

procedure InitClock();
begin
  // смещение каждого разряда часов относительно предыдущего
  ySlide[0] := 0; 
  ySlide[1] := 5;
  ySlide[2] := 6;
  ySlide[3] := 5;
  ySlide[4] := 6;
  ySlide[5] := 5;
  ySlide[6] := 10;
  ySlide[7] := 6;
  ySlide[8] := 5;
end;

procedure InitTrain();
var VagonLength, TailVagonLength, HeadWheelShift, VagonWheelBase: extended;
begin
  VagonLength := 287.7;
  TailVagonLength := 290;
//  HeadWheelShift := 0;
  VagonWheelBase := 157.5;
  WheelBase := 44;
  MinTime := -8;
  MaxTime := 21;

  // координаты тележек
  tel1[0] := 0;
  tel2[0] := -VagonWheelBase;

  tel1[1] := -VagonLength;
  tel2[1] := tel1[1] - VagonWheelBase;

  tel1[2] := -VagonLength * 2;
  tel2[2] := tel1[2] - VagonWheelBase;

  tel1[3] := -VagonLength * 3; //- HeadWheelShift + TailVagonLength;
  tel2[3] := tel1[3] - VagonWheelBase {- HeadWheelShift};

  VagonName[0] := 'Head';
  VagonName[1] := 'Middle';
  VagonName[2] := 'Middle';
  VagonName[3] := 'Tail';
end; // InitTrain

procedure DrawClock(Departure: integer);
var i: integer;
    Hour, Mn, sec, ms: word;
begin
  // рисуем часы

  DecodeTime(Time, Hour, Mn, Sec, MS);

  Digit[0] := Hour div 10;
  Digit[1] := Hour mod 10;
  Digit[2] := Mn div 10;
  Digit[3] := Mn mod 10;
  Digit[4] := Sec div 10;
  Digit[5]:= Sec mod 10;

// время отправления предыдущего поезда - три последние цифры
  if Departure >= 600 then begin
    Digit[6] := -1;
    Digit[7] := -1;
    Digit[8]:= -1;
  end else begin
    Mn := Departure div 60;
    Departure := Departure mod 60;
    Digit[6] := Mn;
    Digit[7] := Departure div 10;
    Digit[8]:= Departure mod 10;
  end;

  Draw3dObject('Clock.ClockBody');

  // цифры
  glPushMatrix;
  for i := 0 to 8 do
    if (Digit[i] >= 0) and (Digit[i] <= 9) then begin
      glTranslatef(0, -ySlide[i], 0);
      Draw3dObject('Clock.Digit' + IntToStr(Digit[i]));
    end;
  glPopMatrix;
end; // DrawClock

function GetPeriod(Hour: integer): integer;
var P: integer;
begin
  // период между поездами по часам в сутках
  if      Hour < 2 then  P := 10
  else if Hour < 5 then  P := 10000
  else if Hour < 7 then  P := 5
  else if Hour < 10 then P := 1
  else if Hour < 17 then P := 2
  else if Hour < 20 then P := 1
  else if Hour < 21 then P := 2
  else if Hour < 22 then P := 3
  else                   P := 5;

  Result := P * 60; // переводим минуты в секунды
end; // GetPeriod

procedure CalcWheel(TrainNumber: integer; 
                    L: extended;
                    var WheelX: extended;
                    var WheelY: extended;
                    var WheelZ: extended;
                    var AngleXY: extended;
                    var AngleZ: extended);
var Segment: integer;
    SegmentPart: extended;
begin
  // ищем, в каком сегменте
  Segment := Path2Segment(TrainNumber, L);
  // рассчитываем долю сегмента
  if (Segment + 1) mod NodeCount = 0 then begin
    SegmentPart := (L - PathLength[Segment]) / (PathLength[Segment] - PathLength[Segment - 1]);
    WheelX := Xs[Segment] + SegmentPart * (Xs[Segment] - Xs[Segment - 1]);
    WheelY := Ys[Segment] + SegmentPart * (Ys[Segment] - Ys[Segment - 1]);
    WheelZ := Zs[Segment] + SegmentPart * (Zs[Segment] - Zs[Segment - 1]);
    AngleXY := Arctan2(Ys[Segment - 1] - Ys[Segment], Xs[Segment - 1] - Xs[Segment]) * 57.3;
    AngleZ := -Arctan2(Zs[Segment] - Zs[Segment - 1], sqrt(sqr(Xs[Segment] - Xs[Segment - 1]) + sqr(Ys[Segment] - Ys[Segment - 1]))) * 57.3;
  end else begin
    SegmentPart := (L - PathLength[Segment]) / (PathLength[Segment + 1] - PathLength[Segment]);
    WheelX := Xs[Segment] + SegmentPart * (Xs[Segment + 1] - Xs[Segment]);
    WheelY := Ys[Segment] + SegmentPart * (Ys[Segment + 1] - Ys[Segment]);
    WheelZ := Zs[Segment] + SegmentPart * (Zs[Segment + 1] - Zs[Segment]);
    AngleXY := Arctan2(Ys[Segment] - Ys[Segment + 1], Xs[Segment] - Xs[Segment + 1]) * 57.3;
    AngleZ := -Arctan2(Zs[Segment + 1] - Zs[Segment], sqrt(sqr(Xs[Segment + 1] - Xs[Segment]) + sqr(Ys[Segment + 1] - Ys[Segment]))) * 57.3;
  end;
end; // CalcWheel

// процедура рисует путь шариками, в тестовых целях
procedure DrawPath(r: integer);
var i, j: integer;
    X, Y, Z, AXY, AZ: extended;
begin
  for j := 0 to TrainCount - 1 do begin
    for i := 0 to NodeCount - 1 do
      DrawSphere(xs[i + j * NodeCount], ys[i + j * NodeCount], zs[i + j * NodeCount], r);
    if DoClipHead[j] then begin
      CalcWheel(j, ClipHead[j], X, Y, Z, AXY, AZ);
      glPushMatrix;
      glTranslatef(X, Y, Z);
      glRotatef(AXY, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
      EnableClipPlane(-1, 0, 0, 0);
      glPopMatrix;
      DrawSphere(X, Y, Z, r * 2);
    end;
    DisableClipPlane;
    if DoClipTail[j] then begin
      CalcWheel(j, ClipTail[j], X, Y, Z, AXY, AZ);
      glPushMatrix;
      glTranslatef(X, Y, Z);
      glRotatef(AXY, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
      EnableClipPlane(1, 0, 0, 0);
      glPopMatrix;
      DrawSphere(X, Y, Z, r * 2);
    end;
    DisableClipPlane;
  end;
end; // DrawPath

function CalcDoor(dt: extended): extended;
begin
  // рассчитываем положение дверей 0 - закрыто, 1 - открыто
  if (dt > 0.5) and (dt <= 1.5) then
    Result := (dt - 0.5) ^ 2
  else if (dt > 1.5) and (dt <= StopTime - 2) then
    Result := 1
  else if (dt > StopTime - 2) and (dt <= StopTime - 1) then
    Result := 1 - (dt - (StopTime - 2)) ^ 2
  else if (dt > StopTime - 0.9) and (dt <= StopTime - 0.7) then
    Result := (0.01 - (dt - (StopTime - 0.8)) ^ 2) * 4
  else
    Result := 0;
end; // CalcDoor

function CalcTrain(dt: extended; typ: integer): extended;
// typ: 0 - стоит на платформе, 1 - обычный, 2 - обратный, т.е. тупик, 3 - проходной
var L: extended;
begin
  // рассчитываем торможение, стоянку и разгон. Стоянка - с 0-й до 6-й сек.
  if typ = 0 then
    L := 0
  else if (typ = 1) or (typ = 2) then begin
    if dt < -3 then
      L := - dt ^ 2 - 2 * dt - 5
    else if dt < -2 then
      L := 4 * dt + 4
    else if dt < 0 then
      L := - dt ^ 2
    else if dt < StopTime then
      L := 0               // стоянка
    else
      L := (dt - StopTime) ^ 1.5; // разгон
    if (typ = 2) and (L < 0) then
      L := -L;
  end else if typ = 3 then
    L := 4 * dt + 4;
  Result := L;
end; // CalcTrain

procedure DrawTrainClock(TrainNumber: integer);
var Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1: extended;
begin
  // часы
  if ShowClock[TrainNumber] then begin
    CalcWheel(TrainNumber, Clocks[TrainNumber], Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1);
    glPushMatrix;
    glTranslatef(Wheel1X, Wheel1Y, Wheel1Z + 130);
    glRotatef(AngleXY1, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
    DrawClock(t - tDeparture[TrainNumber]);
    glPopMatrix;
  end;
end; // DrawTrainClock

procedure DrawSingleTrain(dt: extended; TrainNumber: integer; LeftOpened: Boolean; typ: integer);
var dDoor, LHead: extended;
    i, SegmentHead, SegmentTail: integer;
    Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1: extended;
    Wheel2X, Wheel2Y, Wheel2Z, AngleXY2, AngleZ2: extended;
    Wheel3X, Wheel3Y, Wheel3Z, AngleXY3, AngleZ3: extended;
    StraightTrain: Boolean;
begin
  // запоминаем, когда поезд отправился, для часов
  if (dt > StopTime + 3) and (tDeparture[TrainNumber] < tArrive[TrainNumber]) then
    tDeparture[TrainNumber] := Round(t) + 0.5;

  LHead := CalcTrain(dt, typ);

  // обрезка
  if (LHead > 0) and DoClipHead[TrainNumber] then begin
    CalcWheel(TrainNumber, ClipHead[TrainNumber], Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1);
    glPushMatrix;
    glTranslatef(Wheel1X, Wheel1Y, Wheel1Z);
    glRotatef(AngleXY1, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
    EnableClipPlane(-1, 0, 0, 0);
    glPopMatrix;
  end else if (LHead < 0) and DoClipTail[TrainNumber] then begin
    CalcWheel(TrainNumber, ClipTail[TrainNumber], Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1);
    glPushMatrix;
    glTranslatef(Wheel1X, Wheel1Y, Wheel1Z);
    glRotatef(AngleXY1, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
    EnableClipPlane(1, 0, 0, 0);
    glPopMatrix;
  end;

  // при помощи коэффициентов приводим к масштабу
  LHead := -50 * LHead + Stops[TrainNumber];
  dDoor := CalcDoor(dt) * 18;

  // определяем, рисовать ли поезд повагонно, или целиком.
  StraightTrain := NodeCount = 2;
  if not StraightTrain then begin
    SegmentHead := Path2Segment(TrainNumber, LHead - tel1[0] - WheelBase / 2);
    SegmentTail := Path2Segment(TrainNumber, LHead - tel2[VagonCount - 1] + WheelBase / 2);
    if SegmentTail = NodeCount * (TrainNumber + 1) - 1 then
      SegmentTail := NodeCount * (TrainNumber + 1) - 2;
    StraightTrain := SegmentHead = SegmentTail;
  end;

  if not StraightTrain then
    for i := 0 to VagonCount - 1 do begin
      // 1-я тележка
      CalcWheel(TrainNumber, LHead - tel1[i] + WheelBase / 2, Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1);
      CalcWheel(TrainNumber, LHead - tel1[i] - WheelBase / 2, Wheel2X, Wheel2Y, Wheel2Z, AngleXY2, AngleZ2);
      AngleXY1 := Arctan2(Wheel2Y - Wheel1Y, Wheel2X - Wheel1X) * 57.3;
      AngleZ1 := -Arctan2(Wheel2Z - Wheel1Z, sqrt(sqr(Wheel2X - Wheel1X) + sqr(Wheel2Y - Wheel1Y))) * 57.3;
      Wheel1X := (Wheel1X + Wheel2X) / 2;
      Wheel1Y := (Wheel1Y + Wheel2Y) / 2;
      Wheel1Z := (Wheel1Z + Wheel2Z) / 2;

      glPushMatrix;
      glTranslatef(Wheel1X, Wheel1Y, Wheel1Z);
      glRotatef(AngleXY1, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
      glRotatef(AngleZ1, 0, 1, 0); // Поворачиваем сцену на угол вокруг оси Y
      Draw3dObject('Metro.Tel');
      glPopMatrix;

      // 2-я тележка
      CalcWheel(TrainNumber, LHead - tel2[i] + WheelBase / 2, Wheel2X, Wheel2Y, Wheel2Z, AngleXY2, AngleZ2);
      CalcWheel(TrainNumber, LHead - tel2[i] - WheelBase / 2, Wheel3X, Wheel3Y, Wheel3Z, AngleXY3, AngleZ3);
      AngleXY2 := Arctan2(Wheel3Y - Wheel2Y, Wheel3X - Wheel2X) * 57.3;
      AngleZ2 := -Arctan2(Wheel3Z - Wheel2Z, sqrt(sqr(Wheel3X - Wheel2X) + sqr(Wheel3Y - Wheel2Y))) * 57.3;
      Wheel2X := (Wheel2X + Wheel3X) / 2;
      Wheel2Y := (Wheel2Y + Wheel3Y) / 2;
      Wheel2Z := (Wheel2Z + Wheel3Z) / 2;
      glPushMatrix;
      glTranslatef(Wheel2X, Wheel2Y, Wheel2Z);
      glRotatef(AngleXY2, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
      glRotatef(AngleZ2, 0, 1, 0); // Поворачиваем сцену на угол вокруг оси Y
      Draw3dObject('Metro.Tel');
      glPopMatrix;

      // вагон
      glPushMatrix;
      glTranslatef(Wheel1X, Wheel1Y, Wheel1Z);
      glRotatef(Arctan2(Wheel1Y - Wheel2Y, Wheel1X - Wheel2X) * 57.3, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
      glRotatef(-Arctan2(Wheel1Z - Wheel2Z, sqrt(sqr(Wheel1X - Wheel2X) + sqr(Wheel1Y - Wheel2Y))) * 57.3, 0, 1, 0); // Поворачиваем сцену на угол вокруг оси Y

      if dDoor = 0 then
        Draw3dObject('Metro.' + VagonName[i] + 'Vagon.Closed')
      else begin
        if LeftOpened then begin
          // поезд
          Draw3dObject('Metro.' + VagonName[i] + 'Vagon.LeftOpened');

          // рисуем двери
          glTranslatef(dDoor, 0, 0); // Левая дверь
          Draw3dObject('Metro.' + VagonName[i] + 'Vagon.dverLL');
          glTranslatef(-2 * dDoor, 0, 0); // Правая дверь
          Draw3dObject('Metro.' + VagonName[i] + 'Vagon.dverLR');
        end else begin
          // поезд
          Draw3dObject('Metro.' + VagonName[i] + 'Vagon.RightOpened');

          // рисуем двери
          glTranslatef(dDoor, 0, 0); // Левая дверь
          Draw3dObject('Metro.' + VagonName[i] + 'Vagon.dverRR');
          glTranslatef(-2 * dDoor, 0, 0); // Правая дверь
          Draw3dObject('Metro.' + VagonName[i] + 'Vagon.dverRL');
        end;
      end;
      glPopMatrix;
    end
  else begin
      // поезд
    CalcWheel(TrainNumber, LHead - Tel1[0], Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1);
    glPushMatrix;
    glTranslatef(Wheel1X, Wheel1Y, Wheel1Z);
    glRotatef(AngleXY1, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
    glRotatef(-AngleZ1, 0, 1, 0); // Поворачиваем сцену на угол вокруг оси Y
    if dDoor = 0 then
      Draw3dObject('Metro.Train.Closed')
    else begin
      if LeftOpened then begin
        // поезд
        Draw3dObject('Metro.Train.LeftOpened');
        // двери
        glTranslatef(dDoor, 0, 0); // Левая дверь
        Draw3dObject('Metro.dverLL');
        glTranslatef(-2 * dDoor, 0, 0); // Правая дверь
        Draw3dObject('Metro.dverLR');
      end else begin
        // поезд
        Draw3dObject('Metro.Train.RightOpened');
        // двери
        glTranslatef(dDoor, 0, 0); // Левая дверь
        Draw3dObject('Metro.dverRR');
        glTranslatef(-2 * dDoor, 0, 0); // Правая дверь
        Draw3dObject('Metro.dverRL');
      end;
    end;

    glPopMatrix;
  end;

  // отключаем обрезку
//  if DoClip[TrainNumber] then
  DisableClipPlane;
end; // DrawSingleTrain

procedure DrawTrain(TrainNumber: integer; LeftOpened: Boolean; typ: integer);
var dt: extended;
begin
  DrawTrainClock(TrainNumber);

  dt := t - tArrive[TrainNumber];

  if typ = 0 then
    dt := 3;

  if (dt < MinTime) or (dt > MaxTime) then
    exit;

  DrawSingleTrain(dt, TrainNumber, LeftOpened, typ);
end; // DrawTrain

procedure GetInitPositions;
var i: integer;
begin
  for i := 0 to TrainCount - 1 do begin
    tDeparture[i] := Round(t - Period * Random(1));
    tArrive[i] := tDeparture[i] - StopTime - 3;
  end;
end; // GetInitPositions

procedure GetPositionAbovePeriod;
var i: integer;
begin
  for i := 0 to TrainCount - 1 do
    if t > tArrive[i] + StopTime / 2 + Period / 2 then
      tArrive[i] := Round(tArrive[i] + Period + (Random(1) * 2 - 1) * Period * 0.25)
    else if t < tArrive[i] - Period then
      tArrive[i] := tArrive[i] - 86400;
end; // GetPositionAbovePeriod

procedure DrawTrains(LeftOpened: Boolean; typ: integer);
var i: integer;
begin
  for i := 0 to TrainCount - 1 do
    DrawTrain(i, LeftOpened, typ);
end; // DrawTrains

procedure DrawEscalatorStairs(Hour: integer; t: extended);
begin
  if (Hour >= 5) or (Hour = 0) then begin
    // ступени эскалатора, скорость - 1 ступень в секунду. Frac(t * v)
    DrawStairs('SUp', 1 - Frac(t));
    DrawStairs('SDown', Frac(t));
  end else begin
    DrawStairs('SUp', 0);
    DrawStairs('SDown', 0);
  end;
end;
